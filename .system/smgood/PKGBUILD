# Maintainer: FichteFoll <fichtefoll2@googlemail.com>
pkgname=smgood
pkgver=6.3
pkgrel=5
pkgdesc="Systemd tmpfiles configuration to create a FIFO at /run/tcpdump/genshin"
arch=('any')
license=('none')
depends=('systemd' 'tcpdump')
makedepends=('git' 'go')
# Adjust path as needed.
source=(
  "${pkgname}"::git+file:///home/fichte/code/%23mirror/smgood#commit=aba10a62640e887e068e1f1729970feca3d6a461
  smgood.conf
  tcpdump-genshin
  smgood-tcpdump.service
  smgood-epochd.service
  smgood.service
  socket-chmod-777.patch
)
sha512sums=(
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
)
install=smgood.install

# https://wiki.archlinux.org/title/Go_package_guidelines
prepare(){
  cd "${pkgname}"
  mkdir -p build/
  # Make socket writable for all users
  # since we run smgood.service as a user service.
  patch -p0 < ../socket-chmod-777.patch
}

build() {
  cd "${pkgname}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -o build ./cmd/smgood ./cmd/epochd
}

package() {
  # Config/meta files
  install -vDm644 -t "${pkgdir}/usr/lib/tmpfiles.d/" smgood.conf
  install -vDm644 -t "${pkgdir}/etc/systemd/system/" smgood-tcpdump.service
  install -vDm644 -t "${pkgdir}/etc/systemd/system/" smgood-epochd.service
  install -vDm644 -t "${pkgdir}/etc/systemd/user/"   smgood.service

  # Executables
  install -vDm755 -t "${pkgdir}/usr/bin/" tcpdump-genshin
  install -vDm755 -t "${pkgdir}/usr/bin/" "${pkgname}"/build/smgood
  install -vDm755 -t "${pkgdir}/usr/bin/" "${pkgname}"/build/epochd
}
